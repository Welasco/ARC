resources
| where type == 'microsoft.azurearcdata/sqlserverinstances'
| extend Version = properties.version
| extend Edition = properties.edition
| extend containerId = tolower(tostring (properties.containerResourceId))
| where Version == "SQL Server 2022"
//| where Edition in ("Enterprise", "Standard")
| where isnotempty(containerId)
| project containerId, SQL_instance = tostring(properties.instanceName), Version, Edition
| join kind=inner (
    resources
    | where type == "microsoft.hybridcompute/machines"
    | extend machineId = tolower(tostring(id))
    | project machineId, Machine_name = name
)on $left.containerId == $right.machineId
| project containerId, SQL_instance, Version, Edition, machineId, Machine_name
| order by machineId asc
| join kind=inner(
    resources
    | where type == tolower('Microsoft.HybridCompute/machines/extensions')
    | where name == 'WindowsAgent.SqlServer'
    | mv-expand with_itemindex = i properties.settings.AzureAD
    | extend instanceName = tostring(properties_settings_AzureAD.instanceName), aadkeyVaultCertificateName = properties_settings_AzureAD.aadkeyVaultCertificateName, appRegistrationName = properties_settings_AzureAD.appRegistrationName, azureCertSecretId = properties_settings_AzureAD.azureCertSecretId, adminLoginName = properties_settings_AzureAD.adminLoginName, adminLoginType = properties_settings_AzureAD.adminLoginType
    | project instanceName, aadkeyVaultCertificateName, appRegistrationName, azureCertSecretId, adminLoginName, adminLoginType
    | where instanceName <> ""
)on $left.SQL_instance == $right.instanceName